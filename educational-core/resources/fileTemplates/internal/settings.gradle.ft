static String sanitizeName(String name) {
    return name.replaceAll("[ /\\\\:<>\"?*|]", "_")
}

rootProject.name = sanitizeName('$PROJECT_NAME$')



def lessons = rootProject.projectDir.listFiles(new FilenameFilter() {
    @Override
    boolean accept(File dir, String name) {
        def ignoredNames = ["gradle", "build", "out", "util"]
        return new File(dir.path, name).isDirectory() && !ignoredNames.contains(name) && !name.startsWith(".")
    }
})

for (lesson in lessons) {
    includeLesson(lesson)
}

def includeLesson(File lessonDir) {
    def tasks = lessonDir.listFiles(new FilenameFilter() {
        @Override
        boolean accept(File dir, String name) {
            def ignoredNames = ["build", "out"]
            return dir.isDirectory() && !ignoredNames.contains(name)
        }
    })
    for (task in tasks) {
        String moduleName = "${sanitizeName(lessonDir.name)}-${sanitizeName(task.name)}"
        include "$moduleName"
        project(":$moduleName").projectDir = new File(lessonDir.name, task.name)
    }
}

include 'util'